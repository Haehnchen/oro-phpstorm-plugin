plugins {
    id "org.jetbrains.intellij" version "0.0.43"
    id "de.undercouch.download" version "2.1.0"
}

import de.undercouch.gradle.tasks.download.Download

group 'com.oroplatform'
version '1.0.0-alpha11'

apply plugin: 'org.jetbrains.intellij'
apply plugin: 'java'
apply plugin: 'groovy'

def phpPluginVersion = "php-143.382.38"

sourceCompatibility = 1.6

intellij {
    version project.hasProperty('ideaVersion') ? ideaVersion : 'IU-2016.1'
    type 'IU'
    pluginName 'idea-oroplatform-plugin'
    plugins = ['php', 'yaml', 'java-i18n', 'properties', 'CSS', 'JavaScriptLanguage']
    updateSinceUntilBuild false
    sameSinceUntilBuild false
    downloadSources !Boolean.valueOf(System.getenv('CI'))
    sandboxDirectory "${project.rootDir}/.idea-sandbox"
}

repositories {
    mavenCentral()
}

def phpPluginFiles(String version) {
    fileTree(dir: "${project.intellij.ideaDirectory}/plugins/$version/php/lib/", include: '*.jar')
}

afterEvaluate {
    it.tasks.findByName("compileJava").dependsOn("downloadAndUnzipPhpPlugin")
    it.intellij.intellijFiles.addAll(phpPluginFiles(phpPluginVersion).getFiles())

    project.tasks.create(name: "downloadPhpPlugin", type: Download) {
        def output = new File("${project.intellij.ideaDirectory}/plugins/${phpPluginVersion}.zip")

        src "https://plugins.jetbrains.com/files/6610/22244/${phpPluginVersion}.zip"
        dest output
        overwrite false
    }

    project.tasks.create(name: "downloadAndUnzipPhpPlugin", dependsOn: "downloadPhpPlugin", type: Copy) {
        from zipTree(downloadPhpPlugin.dest)
        into "${project.intellij.ideaDirectory}/plugins/$phpPluginVersion"
    }

    dependencies {
        compile phpPluginFiles(phpPluginVersion)
    }
}
